.apply(function(){
	if(.tmp.ruler==null)
		.tmp.ruler = .Map();
	if(.tmp.ruler.secretary==null)
		.tmp.ruler.secretary=.Map();
	if(.tmp.ruler.archive==null)
		.tmp.ruler.archive = .Array();
	if(.tmp.ruler.criminals==null)
		.tmp.ruler.criminals = .Array();
	
	this = .tmp.ruler.secretary;

	if(.buildplot)
		Secretary = .get_mob_index(4285); 
	else
		Secretary = .get_mob_index(4286); 
	JudePlacement = .get_obj_index(4282);
	CriminalBench = .get_obj_index(4283);
	

	Secretary.onCommand = function(mob, ch, cmdName, cmdArgs){
		//в отключке
		if(mob.position < .tables.position_table.rest)
			return false;
		//дерусь
		if(mob.fighting!=null)
			return false;
	
		//команду ввел не рулер
		if(ch.clan.name!='ruler') {
			return false;
		}
		//команда не "сесть"
		if(cmdName!='sit') {
			return false;
		}
		//уже сидит на кафедре
		if(ch.on != null && ch.on.pIndexData==.tmp.ruler.secretary.JudePlacement && ch.position==.tables.position_table.sit) {
			return false;
		}
		//процесс уже в процессе
		if(mob.process!=null 
				&& mob.process.thread!=null 
				&& mob.process.thread.running)
			return false;

		mob.process = .tmp.ruler.secretary.Process(mob, ch);
		mob.process.thread.start();
	};
	Secretary.onSpeech = function(mob, ch, msg) {
		if(ch.clan.name!='ruler' || ch.on == null 
			|| ch.on.pIndexData!=.tmp.ruler.secretary.JudePlacement 
			|| ch.position!=.tables.position_table.sit
		) return false;

		if(!mob.expectSpeech) return false;
		
		mob.speech = msg;
		mob.expectSpeech = false;
	};


	Process = function(mob, ch) {
		this = .Map();

		this.mob = mob;
		this.ch = ch;
		mob.task = .Map();
		
		thread = .scheduler.Thread(function(this) {
			try {
				run();
			} catch (e) {
				if(e == "sleep: ch left the room" || e == "sleep: someone is dead" || e == "sleep: ch not in jude placement") {
					// персонаж ушел, или кто-то скончался - это норма!
					debug("rulerSecretary.Process normal: " + e);
				} else {
					debug("rulerSecretary.Process: " + e);
				}
				cleanup();
			}
			//cleanup();
		}, this);

		thread.name = .fmt("%C1 process for %C1", mob, ch);  
		
		run = function() {
			wait(0, mob, ch);

			say("Начинам судебное заседание.",1);
			
			taskRuler();

			taskCriminal();

			taskCrime();

			taskVictim();

			taskPlace();

			taskPunishment();

			taskArchive();

			report();

			say("Судебное заседание закончено!",1);

			social_vict("smile",0);

			done();
		};

		taskRuler = function() {
			say(.fmt("Заседание ведёт %C1.",ch),1);

			mob.task.ruler = .Map();
			mob.task.ruler.id = ch.id; 
			mob.task.ruler.name = '';
			mob.task.ruler.runame = ch.name;
			if(ch.russianName!='')
				mob.task.ruler.runame = ch.russianName;
			//remort
			if(ch.remort_count!=null && ch.remort_count>0) {
				mob.task.ruler.name = .fmt("{1{W({M%d{W){2 ", ch.remort_count);
			}
			//pretitle
			if(ch.pretitle!=null && ch.pretitle!="") {
				mob.task.ruler.name = .fmt("%s{1%s{2 ", mob.task.ruler.name, ch.pretitle);
			}
			//name
			mob.task.ruler.name = .fmt("%s{1{W{nr%s{ne%s{nx{2", mob.task.ruler.name, mob.task.ruler.runame, ch.name).trim();

			//title
			if(ch.title!=null && ch.title!="") {
				mob.task.ruler.name = mob.task.ruler.name + " {1";
				try {
					mob.task.ruler.name = mob.task.ruler.name + ch.getParsedTitle();
				} catch(e) {
					mob.task.ruler.name = mob.task.ruler.name + ch.title;
				}
				mob.task.ruler.name = mob.task.ruler.name + "{2";
			}
		};

		taskArchive = function() {
			var map;
			mob.task.case = getNextIndex(.tmp.ruler.archive.keys);
			map = .Map();
			map.date = .Map();
			map.date.world_time = .world_time;
			map.date.name = .date();

			map.ruler = mob.task.ruler.name;
			map.ru_ruler = mob.task.ruler.runame;

			map.criminal = .Map();
			map.criminal.id = mob.task.criminal.id;
			map.criminal.name = charTaskToString(mob.task.criminal);
			map.criminal.clan = mob.task.criminal.clan;

			if(.tmp.ruler.criminals[mob.task.criminal.id]==null) {
				var criminal_map;
				criminal_map = .Map();
				criminal_map.name = mob.task.criminal.name; 
				criminal_map.runame = mob.task.criminal.runame;
				criminal_map.remort_count = mob.task.criminal.remort_count; 
				if(mob.task.criminal.sex!=null && mob.task.criminal.race!=null){
					if(mob.task.criminal.sex==2){
						criminal_map.race =  mob.task.criminal.race.nameFemale;
					} else {
						criminal_map.race = mob.task.criminal.race.nameMale;
					} 
				}
				.tmp.ruler.criminals[mob.task.criminal.id] = criminal_map;
				say(.fmt("Заведено персональное дело на %C4.",mob.task.criminal.char),1);
			}

			//проверяем есть ли личное дело
			var where_file_str;
			where_file_str = .tmp.ruler.file.where_file(mob.task.criminal.char.id);
			if(where_file_str==''){
				obj_to_give = .tmp.ruler.file.Init(mob.task.criminal.id);
				give(obj_to_give, 1);
			} else {
				say(where_file_str,1);
			}

			map.crimes = mob.task.crimes.clone();

			map.place = mob.task.place;

			map.punishment = mob.task.punishment.clone();

			.tmp.ruler.archive[mob.task.case] = map;
			say("Копия дела номер " + mob.task.case + " будет передана в архив.",1);
			
			var obj_to_give;
			obj_to_give = .tmp.ruler.case.Init(mob.task.case);
			give(obj_to_give, 1);
		};
		getNextIndex = function(list) {
			if(list.size()==0) return 1;

			return list.back()+1;
		};

		charTaskToString = function(task_char) {
			var result;
			result="";
			
			//clan
			if(task_char.clan!=null) {
				if(task_char.clan.name!="none") {
				result = result + " {1{W[{"
					+ task_char.clan.color
					+ task_char.clan.name.toUpper().at(0) 
					+ "{W]{x{2";
				}
			}
			//remort
			if(task_char.remort_count!=null && task_char.remort_count>0) {
				result = result + " {1{W({M"
				+ task_char.remort_count
				+ "{W){2";
			}
			//pretitle
			if(task_char.pretitle!=null && task_char.pretitle!="") {
				result = result + " {1"
				+ task_char.pretitle
				+ "{2";
			}
			//name
			result = result + " {1{w";
			if(task_char.runame!="") {
				result = result + "{nr"
				+ task_char.runame
				+ "{ne";
			}
			result = result + task_char.name+"{nx{2";
			//title
			if(task_char.title!=null && task_char.title!="") {
				result = result + " {1"
				+ task_char.title
				+ "{2";
			}

			return result.trim();
		};
		report = function () {
			//репортим дело
			var case_map; case_map = .tmp.ruler.archive[mob.task.case];
			say(case_map.date.name,0);
			say("Осужденный "+case_map.criminal.name,0);
			say("Место совершения преступления: "+case_map.place, 0);
			say(.fmt("Осужден за %s.", crimesToString(case_map.crimes)),0);
			say(.fmt("Приговорен к %s.", punishmentToString(case_map.punishment)),0);
			say(.fmt("Судья %s.",case_map.ruler),0);
		};
		taskPunishment = function() {
			var punish, jail, judge, fine, confiscate, wanted,execution, additional;
			jail = false; judge = false; fine = false;
			confiscate = false; wanted = false; 
			execution = false; additional = false;
			var punish_str; 
			for(key in mob.task.crimes.keys) {
				punish = .tmp.ruler.Punishments[.tmp.ruler.Crimes[key].punishment];
				punish_str='';
				if(punish.jail!='') {
					jail = true;
					punish_str = .fmt('%N5: ',.tmp.ruler.PunishmentType['jail'].name)+punish.jail;
				}
				if(punish.judge!='') {
					judge = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + .fmt('%N5: ',.tmp.ruler.PunishmentType['judge'].name)+punish.judge;
				}
				if(punish.fine!='') {
					fine = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + .fmt('%N5: ',.tmp.ruler.PunishmentType['fine'].name)+punish.fine + " золотых";
				}
				if(punish.confiscate) {
					confiscate = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + .fmt('%N5',.tmp.ruler.PunishmentType['confiscate'].name);
				}
				if(punish.wanted) {
					wanted = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + .fmt('%N5',.tmp.ruler.PunishmentType['wanted'].name);
				}
				if(punish.execution) {
					execution = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + .fmt('%N5',.tmp.ruler.PunishmentType['execution'].name);
				}
				if(punish.additional!='') {
					additional = true;
					if(punish_str!='') punish_str = punish_str + ', ';
					punish_str = punish_str + punish.additional;
				}
				say(.fmt('Преступление, предусмотренное статьёй %s, может караться %s.',key,punish_str),1);
			}
			mob.task.punishment=.Array();
			var yesno;
			for(;mob.task.punishment.keys.size()==0;) {
				if(jail) {
					for(;mob.task.punishment['jail']==null;) {
						say("Cрок заключения подсудимого?",0);
						waitSpeech();
						if(mob.speech.match("[0-9]{1,3}")) {
							mob.task.punishment['jail'] = mob.speech;
							break;
						} 
						say(.fmt('Не понял%Gо||а, что за "%s"?',mob,mob.speech),0);
					}
				}
				if(judge) {
					for(;mob.task.punishment['judge']==null;) {
						say("Изменение законопослушности?",0);
						waitSpeech();
						if(mob.speech.match("-[0-9]{1,3}")) {
							mob.task.punishment['judge'] = mob.speech;
							break;
						}else if(mob.speech.match("[0-9]{1,3}")) {
							mob.task.punishment['judge'] = "-" + mob.speech;
							break;
						}
						say(.fmt('Непонял%Gо||а, что за "%s"?',mob,mob.speech),0);
					}
				}
				if(fine) {
					for(;mob.task.punishment['fine']==null;) {
						say("Штраф?",0);
						waitSpeech();
						if(mob.speech.match("[0-9]{1,4}")) {
							//todo 
							mob.task.punishment['fine'] = mob.speech;
							break;
						}
						say(.fmt('Непонял%Gо||а, что за "%s"?',mob,mob.speech),0);
					}
				}
				if(confiscate) {
					for(;mob.task.punishment['confiscate']==null;) {
						say("Конфискация?",0);
						waitSpeech();
						if(getYesNo(mob.speech)==false) {
							break;
						}
						mob.task.punishment['confiscate']=mob.speech;
					}
				}
				if(wanted) {
					for(;mob.task.punishment['wanted']==null;) {
						say("Объявление в розыск?",0);
						waitSpeech();
						yesno = getYesNo(mob.speech);
						if(yesno==null) {
							say(.fmt('Не понял%Gо||а, что значит "%s"? Да или нет?',mob,mob.speech),0);
							continue; 
						}
						mob.task.punishment['wanted']=yesno;
					}
				}
				if(execution) {
					for(;mob.task.punishment['execution']==null;) {
						say("Смертная казнь?",0);
						waitSpeech();
						yesno = getYesNo(mob.speech);
						if(yesno==null) {
							say(.fmt('Не понял%Gо||а, что значит "%s"? Да или нет?',mob,mob.speech),0);
							continue; 
						}
						mob.task.punishment['execution']=yesno;
					}
				}
				if(additional) {
					for(;mob.task.punishment['additional']==null;) {
						say("Другие наказания?",0);
						waitSpeech();
						if(getYesNo(mob.speech)==false) {
							break;
						}
						mob.task.punishment['additional']=mob.speech;
					}
				}
				var msg; 
				msg = punishmentToString(mob.task.punishment);
				say(.fmt("За совершенное %1$^N1 приговорен к %2$s.", mob.task.criminal.runame, msg), 1);
			}

		};
		punishmentToString = function(punishArray){
			return .tmp.ruler.punishmentToString(punishArray, 3);
		};
		taskPlace = function(){
			for(;mob.task.place==null;) {
				say("Где совершено преступление?",0);
				waitSpeech();
				//TODO придумать проверку места преступления
				mob.task.place=mob.speech;
				
			}
		};
		taskVictim = function() {
			var mob_msg, victims;
			if(mob.task.crimes_vict_count>1) say("Назови потерпевших по пунктам обвинения.",0);

			for(key in mob.task.crimes.keys) {
				if(.tmp.ruler.Crimes[key].victim=="") continue;
				for(;mob.task.crimes[key].victims==null;) {
					say(.fmt("Кто потерпевший по пункту %s?", key),0);
					waitSpeech();
					if(.tmp.ruler.Crimes[key].victim=="pc") {
						mob.task.crimes[key].victims = parsCharsFromSpeech();
					} else if(.tmp.ruler.Crimes[key].victim=="npc") {
						//TODO проверка мобов
						mob.task.crimes[key].victims = mob.speech;
					}

					if(mob.task.crimes[key].victims.size()==0) {
						say(.fmt("Я не знаю никого с именем %^s.",mob.speech),1);
						mob.task.crimes[key].victims = null;
					} 
				}
				if(.tmp.ruler.Crimes[key].victim=="pc") {
					mob_msg = ''; victims="Потерпевший";
					for(victim in mob.task.crimes[key].victims) {
						if(mob_msg!='') {
							mob_msg = mob_msg + ',';
							victims="Потерпевшие";
						}
						mob_msg = .fmt("%s %s", mob_msg, victim);
					}
				} else {
					mob_msg = mob.task.crimes[key].victims; 
					if(mob.task.crimes[key].victims.match('.*, .*')) 
						victims="Потерпевшиe";
					else
						victims="Потерпевший";
				}
				
				say(.fmt("%^s по пункту %s: %s.",victims, key, mob_msg),1);
			}
		};
		taskCrime = function() {
			for(;mob.task.crimes==null;) {
				say(.fmt("По каким пунктам закона обвиняется %^C1?",mob.task.criminal.char),0);
				waitSpeech();
				mob.task.crimes = .Array();
				mob.task.not_crimes = .List();
				mob.task.crimes_vict_count = 0;
				getCrimesFromSpeech();

				if(mob.task.crimes.keys.size()>0 && mob.task.not_crimes.size()==0) {
					break;
				} 
				social_vict("shrug",0);
				if(mob.task.not_crimes.size()>0) {
					if(mob_msg!='' && mob.task.crimes.size()>0) {
						say(.fmt("Можно осудить %^C1 по %s.", mob.task.criminal.char, crimesToString(mob.task.crimes)),1);
					} 
					var mob_msg;
					mob_msg = '';
					for(key in mob.task.not_crimes) {
						if(mob_msg!='') mob_msg = mob_msg+", ";
						mob_msg = mob_msg + key;
					}
					mob_msg = .fmt("нельзя осудить по %s", mob_msg);
					
					if(mob.task.crimes.keys.size()>0) 
						mob_msg =  "Но " + mob_msg;
					else 
						mob_msg = .fmt("%^C1 %s.", mob.task.criminal.char, mob_msg);

					say(mob_msg,1);
				} else {
					say(.fmt('%^C2 нельзя осудить по пункту %s.', mob.task.criminal.char, mob.speech.quote()),1);
				}
				say("Для обвинения доступны следующие статьи Закона:",0);
				for(crime in .tmp.ruler.Crimes.values) {
					say(.fmt("%s. %s",crime.number,crime.name),0);
				}

				mob.task.crimes = null;
				mob.task.not_crimes = null;
				mob.task.crimes_vict_count = null;
				wait(1, mob, ch);

			}
			say(.fmt("%^C1 обвиняется по %s.", mob.task.criminal.char, crimesToString(mob.task.crimes)),1);
		};
		taskCriminal = function() {
			//todo сохранять id и строку
			mob.task.criminal = autoCriminal();
			for(;mob.task.criminal==null;){
				say("Кого обвиняем?",0);
				waitSpeech();
				mob.task.criminal = getCharFromSpeech();

				if(mob.task.criminal!=null) {
					//если не удалось получить ссылку на персонажа (н-р: offline)
					if(mob.task.criminal.char==null) {
						var tmp_name;
						tmp_name = mob.task.criminal.name;
						if(mob.task.criminal.runame!=null && mob.task.criminal.runame!='') tmp_name = mob.task.criminal.runame;
						say(.fmt("Я не ощущаю присутствия %^s нигде в мире.", tmp_name),1);
						mob.task.criminal=null;
						continue;
					}

					if(!mob.task.criminal.char.is_npc())
						break;
					else {
						say(.fmt("Зачем судить %^C4? Просто убей его!!!",mob.task.criminal.char),1);
						mob.task.criminal=null;
						continue;
					}
				} 

				say(.fmt("Я не знаю никого с именем %^s",mob.speech),1);
			}
			if(mob.task.criminal.runame!="")
				say(.fmt("Обвиняемый - %^N1.", mob.task.criminal.runame),1);
			else
				say(.fmt("Обвиняемый - %^N1.", mob.task.criminal.name),1);
		};

		getCrimesFromSpeech = function() {
			var speech;
			speech = mob.speech.replace(","," ");
			speech = speech.replace(";"," ");
			for(text in speech.arguments()){
				if(text.match("([0-9]{1}\.[0-9]{1}\.[0-9]{1}|[0-9]{1}\.[0-9]{1})")) {
					for(num in text.matchGroups("([0-9]{1}\.[0-9]{1}\.[0-9]{1}|[0-9]{1}\.[0-9]{1})")) {
						if(.tmp.ruler.Crimes[num]!=null){ 
							mob.task.crimes[num] = .Map();
							mob.task.crimes[num].number = num;
							if(.tmp.ruler.Crimes[num].victim!='') {
								mob.task.crimes_vict_count = mob.task.crimes_vict_count + 1;
							}
						} else {
							mob.task.not_crimes.push_back(num);
						}
					}
				} else {
					mob.task.not_crimes.push_back(text);
				}
			}
		};
		getCharFromSpeech = function(){
			var char, char_map;
			char_map = .Map();
			char = .get_char_world(mob.speech);

			if(char==null) {
				if(.player_exists(mob.speech)){
					char_map.name = .player_name(mob.speech);
					char_map.runame = .player_russian_name(mob.speech);
					char_map.clan = .Clan(.player_clan(mob.speech));
				} else {
					return null;
				}
			} else {
				char_map.char = char;
				if(!char.is_npc()) {
					char_map.id = char.id;
					char_map.name = char.name;
					char_map.runame = char.russianName; //падежи
					char_map.sex = char.sex;
					char_map.race = char.race; //структура
					char_map.pretitle = char.pretitle;
                    try {
                        char_map.title = char.getParsedTitle();
                    } catch(e) {
                        char_map.title = char.title;
                    }
					    
					char_map.remort_count = char.remort_count;
					char_map.clan = char.clan; //структура
				}
			}

			return char_map;
		};
		parsCharsFromSpeech = function() {
			var speech, victim, result, tmp_speech;
			tmp_speech = mob.speech;
			result = .List();
			speech = mob.speech.replace(","," ");
			speech = speech.replace(";"," ");
			for(chname in speech.arguments()) {
				mob.speech = chname;
				victim = getCharFromSpeech();
				if(victim!=null){
					if(victim.char!=null && victim.char.is_npc()) {
						say(.fmt("%^C1 не является персонажем и не может быть потерпевшим по данной статье.",victim.char));
						result = null;
						break;
					} else {
						result.push_back(charTaskToString(victim));
					}
				}
			}
			mob.speech = tmp_speech;
			return result;
		};
		crimesToString = function(crimes) {
			return .tmp.ruler.crimesToString(crimes);
		};
		autoCriminal = function() {
			//2DO criminal = char in manacle;
			return null;
		};
		getYesNo = function(msg) {
			if(msg.match("^да$|^yes$|^yepe$|^угу$|^ага$"))
				return true;
			else if(msg.match("^no$|^nope$|^нет$|^не-*а$"))
				return false;
			else 
				return null;

		};
		waitSpeech = function() {
			for(;;) {
				mob.speech = null;
				mob.expectSpeech = true;
				
				for(i=0; mob.speech == null; i = i + 1)
					wait(1,mob,ch);

				break;
			}
		};

 
		cleanup = function() {
			thread.stop();
			mob.process = null;
		};
		wait = function(seconds, mob, ch) {
			.scheduler.sleep(seconds * 4);
			if (mob.dead || ch.dead)
				throw "sleep: someone is dead";
			if (mob.in_room != ch.in_room)
				throw "sleep: ch left the room";
			if(ch.on == null 
				|| ch.on.pIndexData!=.tmp.ruler.secretary.JudePlacement 
				|| ch.position!=.tables.position_table.sit)
				throw "sleep: ch not in jude placement";
			//TODO сохранение незавершенного дела
		};
		say = function(msg, pause) {
			.tmp.quest.say(msg, mob, ch);
			wait(pause, mob, ch);
		};
		emote = function(msg, pause) {
			.tmp.quest.emote(msg, mob);
			wait(pause, mob, ch);
		};
		give = function(obj, pause) {
			wait(0, mob, ch);
			ch.act("%^C1 дает тебе %O4.", mob, obj);
			mob.act("Ты даешь %O4 %^C3.", obj, ch);
			ch.rvecho(mob,"%^C1 даёт %O4 %^C3.", mob, obj, ch);
			obj.obj_to_char(ch);
			wait(pause, mob, ch);
		};
		tell = function(msg, pause) {
			.tmp.quest.tell(msg, mob, ch);
			wait(pause, mob, ch);
		};
		social_vict = function(social, pause) {
			mob.interpret_cmd(social, ch.name);
			wait(pause, mob, ch);
		};
		done = function() {
			debug("done...");
			mob.process=null;
		};
		debug = function(str) {
			if(.buildplot) {
				var vict;
				vict = .get_char_world("Miyamoto");
				if(vict==null) vict = .get_char_world("Zaleshanin");
				if(vict!=null) vict.act("rulers secretary debug: %s",str);
			}
		};
		
		return this;
	};

	
})