.apply(function(){
    if(.tmp.ruler==null) 
        .tmp.ruler = .Map();
    if(.tmp.ruler.case==null) 
        .tmp.ruler.case = .Map();

    this = .tmp.ruler.case;

    //прототип дела
    Case = .get_obj_index(4287);
    if(.buildplot) Case = .get_obj_index(3015); //лист бумаги

    Case.onExtraDescr = function(case, ch, str) {
        this = .tmp.ruler;

        if ( archive == null 
            || archive[case.number] == null
            || crimesToString == null 
            || punishmentToString == null ) 
            return '';

        var result;
        result = '';

        //смотрим на case.name
        if(str.is_name(case.name)) {
            var desc; 
            var ruler;
            ruler = "суд|ья|ьи|ье|ью|ьёй|ье";
            if(.tmp.ruler.archive[case.number].ru_ruler!=null)
                ruler = .tmp.ruler.archive[case.number].ru_ruler;

            result = .fmt("Ты смотришь на гербовую бумагу исписанную каллиграфическим подчерком, заверенную несколькими штампами и сургучными печатями разной толщины и подписанную %^N5. Выглядит очень солидно.", ruler).format(80);
            result = result + "\n" + .tmp.ruler.case.onExtraCase(case.number);
            
            if(ch.clan.name=="ruler") {
                result = result + "\n"
                    + "{C({cдля вставки через {1{C{lrвебредактор{lewebedit{lx{2 ({hc{lrсм образец%2$d{lelook pattern{lx{hx), "
                    + "{cдля вставки через {1{C{lrпреступление{lecrime{lx{2 ({hc{lrсм преступление%2$d{lelook crime{lx{hx){C)\n";
                    
            }
            
        }

        if(str.is_name("pattern образец")) {
            result = .tmp.ruler.case.onExtraCase(case.number).replace("{", "{{");
        }

        if(str.is_name("crimenote преступление")) {
            //TODO похожий код в get_case_short
            //TODO добавить номер дела в crime subj

            var criminal, case_criminal;
            //айди, клан
            case_criminal = .tmp.ruler.archive[case.number].criminal;
            //реморт, имена
            criminal = .tmp.ruler.criminals[case_criminal.id];

            var crimes_str;
            crimes_str = '';
            for(key in .tmp.ruler.archive[case.number].crimes.keys) {
                if(crimes_str!="") crimes_str = crimes_str + ", ";
                crimes_str = crimes_str + key;
            }

            result = criminal.name;
			if(criminal.runame!="") {
				result = .fmt("{nr%s{ne%s{nx",criminal.runame, result);
			}

            if(case_criminal.clan!=null && case_criminal.clan.name!="none") {
                result = .fmt("{%s%s{x", case_criminal.clan.color, result);
            }

            if(criminal.remort_count)
                result = .fmt("{W({M%d{W){x %s {W({w%s{W{x", criminal.remort_count, result, crimes_str);


            result = .fmt("crime subj %s\n", result);
            result = result + "crime to all\n";

            for(case_str in .tmp.ruler.case.onExtraCase(case.number).split("\n")) {
                result = result + "crime + " + case_str + "\n";
            }
            result = result.replace("{", "{{");
        }

        return result;
    };
    get_case_short = function(num) {
        var case, char;
        case = .tmp.ruler.archive[num];
        char = .tmp.ruler.criminals[ case.criminal.id ];

        var clan_color;
        clan_color="";
        if(case.criminal.clan!=null) {
            if(case.criminal.clan.name!="none")
                clan_color = "{" + case.criminal.clan.color;
        }
        var crimes;
        crimes = "";
        for(key in case.crimes.keys) {
            if(crimes!="") crimes=crimes+", ";
            crimes=crimes+key;
        }
        var remort_str;
        remort_str='';
        if(char.remort_count)
            remort_str = .fmt("{W({M%d{W){x ", char.remort_count);
        var chname;
        chname = char.name;
        if(char.runame)
            chname = char.runame;
        return .fmt(
            "номер %d: '{1%s{2{1%s%N1{x{2 (%s)'",
            num,
            remort_str,
            clan_color,
            chname,
            crimes
        );
    };
    Init = function(num) {
        var objCase, char;
        char = .tmp.ruler.criminals[ .tmp.ruler.archive[num].criminal.id ];
        objCase = Case.create();

        objCase.number = num;

        objCase.name = .fmt(
            "%s %d %s %N1", 
            objCase.name, 
            num,
            char.name,
            char.runame
        );

        objCase.short_descr = .fmt(
            "%s %s",
            objCase.short_descr,
            get_case_short(num),
            remort_str,
            clan_color,
            chname,
            crimes
        );

        return objCase;
    };

    /*
    Case - уголовное дело. Объект типа parchment. Имеет уникальный номер.

    Case.onExtraDescr(obj, ch, str)
        "case ###" - выводит тоже самое что crimenote
        "case crimenote" - рулеру выводит заполненный crime 
            (crime subj ..., crime to ..., crime + ...)
        "case template" - заполненный crime для вставки через web edit
    */

})