.apply(function(){
    if(.tmp.ruler==null) 
        .tmp.ruler = .Map();
    if(.tmp.ruler.case==null) 
        .tmp.ruler.case = .Map();

    this = .tmp.ruler.case;

    //прототип дела
    Case = .get_obj_index(4287);
    if(.buildplot) Case = .get_obj_index(3015); //лист бумаги

    Case.onExtraDescr = function(case, ch, str) {
        this = .tmp.ruler;

        if ( archive == null 
            || archive[case.number] == null
            || crimesToString == null 
            || punishmentToString == null ) 
            return '';

        var result;
        result = '';

        var case_map;
        case_map = .tmp.ruler.archive[case.number];       

        //смотрим на case.name
        if(str.is_name(case.name)) {

            result = .fmt("{x Дата:          {c%1$s"
                + "{x Осужденный:    %2$s\n"
                + "{x Место:         {c%3$s\n",
                case_map.date.name,
                case_map.criminal.name,
                case_map.place
            );
            //TODO свой формат.
            for(crime in case_map.crimes.values) {
                result = result + "{x Преступление:  {c";
                var crimeList, ii; ii=0;
                crimeList = .tmp.ruler.formatToList(.tmp.ruler.crimeToString(crime)+'.', 64);
                for(crime_str in crimeList) {
                    if(ii) {
                        result = result + .fmt("%16s", ' ');
                    }
                    result = result + crime_str + "\n";
                    ii=ii+1;
                }
                if(crime.victims!=null) {
                    result = result + .tmp.ruler.victimsToString(crime, "{x %^s:   {x%s{x\n");
                }
            }
            //result = result + .fmt("{x Преступление:  {c%s{x\n", crimesToString(case_map.crimes)).format(80);
 
            result = result + .fmt("{x Наказание:     {1%s{2.\n",punishmentToString(case_map.punishment)).format(80);

            result = result + .fmt("{x Судья:         {1%s{2\n", case_map.ruler);
        }

        return result;
    };

    Init = function(num) {
        var objCase, case, char;
        case = .tmp.ruler.archive[num];
        char = .tmp.ruler.criminals[ case.criminal.id ];
        objCase = Case.create();

        objCase.number = num;

        objCase.name = .fmt(
            "%s %d %s %N1", 
            objCase.name, 
            num,
            char.name,
            char.runame
        );

        var clan_color;
        clan_color="";
        if(case.criminal.clan!=null) {
            if(case.criminal.clan.name!="none")
                clan_color = "{" + case.criminal.clan.color;
        }
        var crimes;
        crimes = "";
        for(key in case.crimes.keys) {
            if(crimes!="") crimes=crimes+", ";
            crimes=crimes+key;
        }
        var remort_str;
        remort_str='';
        if(char.remort_count)
            remort_str = .fmt("{W({M%d{W){x ", char.remort_count);
        var chname;
        chname = char.name;
        if(char.runame)
            chname = char.runame;
        objCase.short_descr = .fmt(
            "%s номер %d: '{1%s{2{1%s%N1{x{2 (%s)'",
            objCase.short_descr,
            num,
            remort_str,
            clan_color,
            chname,
            crimes
        );

        return objCase;
    };

    /*
    Case - уголовное дело. Объект типа parchment. Имеет уникальный номер.

    Case.onExtraDescr(obj, ch, str)
        "case ###" - выводит тоже самое что crimenote
        "case crimenote" - рулеру выводит заполненный crime 
            (crime subj ..., crime to ..., crime + ...)
        "case template" - заполненный crime для вставки через web edit
    */

})