.apply(function(){
    if(.tmp.ruler==null) 
        .tmp.ruler = .Map();
    if(.tmp.ruler.file==null) 
        .tmp.ruler.file = .Map();

    this = .tmp.ruler.file;

    Safe = .get_obj_index(4308);
    //прототип папки
    File = .get_obj_index(4288);
    if(.buildplot) File = .get_obj_index(732); //некрономикон

    File.onExtraDescr = function(file, ch, str) {
        this = .tmp.ruler;

        var result;
        result = '';

        var isClosed, isPrivateFile, isCase, obj_case;
        
        isCase = false;
        isPrivateFile = false;
        isClosed = (file.value1&.tables.container_flags.closed);

        if(str.is_name(file.name)) {
            //смотрим на личное дело
            isPrivateFile = true;
        }

        // если дело закрыто, а смотрим на что-то из содержимого
        if(!isPrivateFile && isClosed) {
            return result;
        }

        var case_num;
        case_num = 0;
        //пытаемся получить номер дела из строки
        if(str.match("[a-zа-я]+([0-9]+)")) {
            case_num = str.matchGroups("[a-zа-я]+([0-9]+)").at(0);
            str = str.replace(case_num,'');
            case_num = 0 + case_num;
        }

        if(case_num != 0 && 'case дело'.match('(^| )'+str) ) { //&& .tmp.ruler.archive[case_num]!=null
            for(item in file.items) {
                if(item.number!=null && item.number==case_num) {
                    obj_case = item;
                    isCase = true;
                    break;
                }
            }
        }

        if(isPrivateFile) {
            if(file.criminal==null)
                return .fmt("Обычн%1Gое|ый|ая %1$O6, ничего особенного.", file);

            var criminal_str, criminal;
            if(.tmp.ruler.criminals[file.criminal]!=null)
                criminal = .tmp.ruler.criminals[file.criminal];
            
            if(criminal==null) {
                criminal_str = file.criminal;
            } else {
                criminal_str = '';

                if(criminal.remort_count)
                    criminal_str = .fmt("{1{W({M%d{W){2 ", criminal.remort_count);

                if(criminal.race!=null)
                    criminal_str = .fmt("%s%s ", criminal_str, criminal.race);
                var criminal_name;
                criminal_name = criminal.name;
                if(criminal.runame) criminal_name = criminal.runame;

                criminal_str = .fmt("%s%s", criminal_str, criminal_name);
            }
            
            if(isClosed) {
                result = .fmt("Ты смотришь на кожаную папку c завязками с тисненым серебром  гербом Ордена Правителей. Ниже также тисненая надпись 'Личное дело:', под которой уже от руки, но очень аккуратно, написано '%N1'.", criminal_str).format(80);
            } else {
                var items_size;
                items_size = file.items.size();

                result = .fmt("Ты заглядываешь в открытую папку с личным делом %N2.", criminal_str);
                var cases, other_list, count_str;
                cases = .Array(); 
                other_list = .List();
                for(item in file.items) {
                    if(item.pIndexData==.tmp.ruler.case.Case) {
                        cases[item.number] = item.number;
                    } else {
                        other_list.push_back(item);
                    }
                }
                
                if(items_size==0) {
                    result = .fmt("%s.. в абсолютно пустую папку.", result).format(80);
                }  else {   
                    if (cases.size()==1) {
                        result = .fmt("%s В папке всего один лист гербовой бумаги,  исписанный каллиграфическим подчерком, как и положено заверенный несколькими штампами и сургучными печатями разной толщины. Выглядит очень солидно.", result).format(80);
                        result = result + "\n" + .tmp.ruler.case.onExtraCase(file.items.at(0).number);
                    } else if (cases.size()>1) {
                        if (cases.size()<=5) {
                            count_str = "небольшая ";
                        } else if (cases.size()<=10) {
                            count_str = "";
                        } else if (cases.size()<=20) {
                            count_str = "внушительная ";
                        } else {
                            count_str = "громадная ";
                        }

                        result = .fmt("%s В папке %sстопка гербовой бумаги, исписанных каллиграфическим подчерком, и заверенных штампами и сургучными печатями разной толщины. Выглядит очень солидно. Ты быстро пролистываешь заголовки страниц:\n", result, count_str).format(80);

                        var cases_list;
                        cases_list = cases.keys.clone();

                        var i;
                        for(i=1;i<=cases.size();i=i+1) {
                            result = result + 
                                .fmt("- дело %1$s ({1{c{hc{lrсм дело%2$d{lelook case%2$d{x{2.)\n", 
                                    .tmp.ruler.case.get_case_short(cases_list.back()),
                                    cases_list.back()
                                );
                            cases_list.pop_back();
                        }
                    }
                    if(other_list.size()>0) {
                        if(other_list.size()==1 && cases.size()==0) {
                            result = .fmt("%1$s.. но внутри не уголовные дела, а %2$Gкакое|какой|какая-то %2$O1.", result, file.items.at(0)).format(80);
                        } else {
                            var other_str;
                            other_str = "Зачем-то в папку кто-то положил ";
                            
                            for(i=0;i<other_list.size();i=i+1) {
                                if(other_list.size()>1 && i==other_list.size()-1) {
                                    other_str = .fmt("%s и", other_str);
                                } else if(i>0) {
                                    other_str = .fmt("%s,", other_str);
                                }
                                other_str = .fmt("%s %O4", other_str, other_list.at(i));
                            }
                            other_str = other_str + ". Возможно ";
                            if(other_list.size()==1) {
                                other_str = other_str + "улика.";
                            } else {
                                other_str = other_str + "улики.";
                            }

                            result = result + "\n" + other_str.format(80);
                        }
                    } 
                }
            }
        }

        if(isCase) {
            result = .fmt("Ты открываешь %1$O4 на нужной странице и изучаешь гербовую бумагу исписанную каллиграфическим подчерком, заверенную, как и положенно, подписью, несколькими штампами и сургучными печатями разной толщины. Выглядит очень солидно.", file).format(80);

            result = result + "\n" + .tmp.ruler.case.onExtraCase(obj_case.number);
        }

        return result;
    };

    File.onCantFetch = function(file, ch, obj, pocket) {
        if(obj==null) return false;
        
        if(obj.pIndexData != .tmp.ruler.case.Case)
            return false;

        if(ch.clan.name != "ruler" && !ch.is_immortal()) {
            ch.act('Хитрая защелка мешает тебе достать %1$O4 из %2$O2.', obj, file);
            return true;
        }
        return false;
    };
    File.onCantPut = function(file, ch, obj, pocket, verbose) {
        if(obj.pIndexData != .tmp.ruler.case.Case) {
            if(ch.clan.name != "ruler") {
                if(verbose) {
                    ch.act("%1$O1 не предназначена для хранения %2$O4.", file, obj);
                }
                return true;
            }
        } else {
            if(obj.number!=null && .tmp.ruler.archive!=null 
                && .tmp.ruler.archive[obj.number]!=null
                && .tmp.ruler.archive[obj.number].criminal.id != file.criminal) {
                if(verbose) {
                    ch.act("%1$^O4 не должно храниться в %2$O6.", obj, file);
                }
                return true;
            }
        }
        return false;
    };
    File.onQuit = function(file, ch, count) {
        if(file.criminal==null) return;

        ch.act('Ты не можешь забрать из мира %O1!', file);
        ch.act('%^O1 исчезает у тебя из рук.', file);
        ch.recho('%^O1 исчезает из рук %^C2.', file, ch);

        if(.tmp.ruler.file.Safe.instances.size()>0)
            file.obj_to_obj(.tmp.ruler.file.Safe.instances.at(0));
        else
            file.obj_to_room(ch.in_room);
    };

    Init = function(id) {
        var file, char;
        char = .tmp.ruler.criminals[ id ];
        file = File.create();

        file.criminal = id;
        file.name = .fmt(
            "%s %s %N1", 
            file.name, 
            char.name,
            char.runame
        );
        var remort_str;
        remort_str='';
        if(char.remort_count)
            remort_str = .fmt("{W({M%d{W){x ", char.remort_count);
        var chname;
        chname = char.name;
        if(char.runame)
            chname = char.runame;

        file.short_descr = .fmt(
            "%s {1%s{2%N2 %N2",
            file.short_descr,
            remort_str,
            char.race,
            chname
        );

        return file;
    };

})